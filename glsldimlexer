%class-header="include/dim/core/scanner.hpp"
%baseclass-header="include/dim/core/scannerbase.hpp"
%lex-source="src/core/lex.cpp"
%implementation-header="src/core/scanner.ih"
%namespace=dim

%x string include comment

%%

// shader stages
^[ \t]*vertex:                return Token::vertex;
^[ \t]*fragment:              return Token::fragment;
^[ \t]*geometry:              return Token::geometry;
^[ \t]*tessControl:           return Token::tessControl;
^[ \t]*tessEvaluation:        return Token::tessEval;
^[ \t]*compute:               return Token::compute;

// preprocessor directives
^[ \t]*#version[ \t]*         return Token::version;

^[ \t]*#include               begin(StartCondition__::include);          

<include>{
  [ \t]*\"                    begin(StartCondition__::string);
}

<string>{
  \"                          {
                                begin(StartCondition__::INITIAL); 
                                size_t quotePos = matched().find_last_of('\"');
                                
                                setMatched(matched().substr(0, quotePos));
                                
                                std::string path = filename().substr(0, filename().find_last_of('/') + 1);

                                pushStream(path + matched());
                                return Token::include;
                              }
                  
  \\.|.                       more();
}

// comments
"/*"                          begin(StartCondition__::comment);

<comment>{
  "*/"                        begin(StartCondition__::INITIAL);
  \\.|.                       
  \n                          return Token::whitespace;
}
       
"//"(\\.|.)*\n                return Token::whitespace;

// end of file
<INITIAL,string,comment><<EOF>> {
                                if(popStream())
                                  return Token::endOfFile;
                                else
                                  return 0;
                              }
			 
// regular GLSL               
layout                        return Token::layout;
location                      return Token::location;

in                            |
attribute                     return Token::in;
out                           return Token::out;

[0-9]*                        return Token::number;
dim_vertex                    {setMatched("0"); return Token::number;}
dim_normal                    {setMatched("1"); return Token::number;}
dim_texCoord                  {setMatched("2"); return Token::number;}
dim_binormal                  {setMatched("3"); return Token::number;}
dim_tangent                   {setMatched("4"); return Token::number;}
dim_instance                  {setMatched("5"); return Token::number;}

[a-zA-Z_][a-zA-Z0-9_]*        return Token::identifier;

[[:space:]]                   return Token::whitespace;
[[:^space:]]                  return matched()[0];
